import java.util.jar.JarFile

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    // https://github.com/johnrengelman/shadow
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

def pyroscopeVersion = project.properties['pyroscope_version']
dependencies {
    api project(":async-profiler-context")
    implementation('com.squareup.okhttp3:okhttp:4.10.0')
    implementation("com.squareup.moshi:moshi:1.14.0")
    implementation("io.kubernetes:client-java:20.0.1-legacy")
    implementation("io.kubernetes:client-java-extended:20.0.1-legacy")
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.13'
    api 'com.google.protobuf:protobuf-java:3.22.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.10.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.10.0'
    constraints {
        implementation('com.nimbusds:nimbus-jose-jwt:9.37.2') {
            because 'Fixes CVE-2023-52428'
        }
        implementation('net.minidev:json-smart:2.4.9') {
            because 'Fixes CVE-2021-31684 and CVE-2023-1370'
        }
    }
}

jar {
    manifest {
        attributes(
                'Premain-Class': 'io.pyroscope.javaagent.PyroscopeAgent'
        )
    }
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    exclude 'Log4j-*'
    exclude 'META-INF/org/apache/logging/log4j/**'
    exclude 'META-INF/services/**'

    from("$buildDir/async-profiler/native") {
        include "**.so"
        include "**.so.sha1"
    }

    archiveFileName = "pyroscope.jar"

    minimize {
        exclude(dependency('org.slf4j:slf4j-simple:.*'))
    }
    archiveClassifier.set('')

}

apply from: "relocate.gradle"

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            groupId = 'io.pyroscope'
            artifactId = 'agent'
            version = pyroscopeVersion
            artifacts = [ shadowJar, javadocJar, sourcesJar ]
            pom {
                name = 'Pyroscope Java agent'
                description = 'The Java profiling agent for Pyroscope.io. Based on async-profiler.'
                url = 'https://pyroscope.io'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pyroscope'
                        name = 'Pyroscope'
                        email = 'anatoly@pyroscope.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pyroscope-io/pyroscope-java.git'
                    developerConnection = 'scm:git:ssh://github.com/pyroscope-io/pyroscope-java.git'
                    url = 'https://github.com/pyroscope-io/pyroscope-java'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('nexusUsername') ? project.nexusUsername : ''
                password project.hasProperty('nexusPassword') ? project.nexusPassword : ''
            }
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    sign publishing.publications.shadow
}

afterEvaluate {
    if (project.tasks.findByName('signShadowPublication')) {
        project.tasks.named('signShadowPublication').configure {
            dependsOn 'jar'
            dependsOn 'sourcesJar'
            dependsOn 'javadocJar'
        }
    }
}